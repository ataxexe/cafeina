/*
 * The MIT License
 *
 * Copyright (c) 2014 Marcelo Guimaraes <ataxexe@gmail.com>
 *
 * ----------------------------------------------------------------------
 * Permission  is hereby granted, free of charge, to any person obtaining
 * a  copy  of  this  software  and  associated  documentation files (the
 * "Software"),  to  deal  in the Software without restriction, including
 * without  limitation  the  rights to use, copy, modify, merge, publish,
 * distribute,  sublicense,  and/or  sell  copies of the Software, and to
 * permit  persons to whom the Software is furnished to do so, subject to
 * the following conditions:
 *
 * The  above  copyright  notice  and  this  permission  notice  shall be
 * included  in  all  copies  or  substantial  portions  of the Software.
 *                        -----------------------
 * THE  SOFTWARE  IS  PROVIDED  "AS  IS",  WITHOUT  WARRANTY OF ANY KIND,
 * EXPRESS  OR  IMPLIED,  INCLUDING  BUT NOT LIMITED TO THE WARRANTIES OF
 * MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.
 * IN  NO  EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY
 * CLAIM,  DAMAGES  OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT,
 * TORT  OR  OTHERWISE,  ARISING  FROM,  OUT OF OR IN CONNECTION WITH THE
 * SOFTWARE   OR   THE   USE   OR   OTHER   DEALINGS  IN  THE  SOFTWARE.
 */

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

configurations {
  project.group = "tools.devnull"
  project.version = "0.1.0"

  project.ext["pomFile"] = "${buildDir}/${project.name}-${project.version}.pom"

  project.description = "Projeto destinado a mapear as diversas situações em " +
      "que nós, programadores, temos que enfrentar em nossos árduos e sombrios " +
      "dias (e noites, e madrugadas, e mais outros dias) de trabalho como papéis " +
      "higiênicos de TI com folha dupla."

  project.targetCompatibility = "1.5"
  project.sourceCompatibility = "1.5"
}

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

task sourcesJar(type: Jar, dependsOn: classes) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

artifacts {
  archives jar
  archives sourcesJar
  archives javadocJar
}

task pom << {
  pom {
    project {
      groupId project.group
      artifactId project.name
      packaging 'jar'

      name project.name
      description project.description

      url 'https://github.com/ataxexe/cafeina'

      licenses {
        license {
          name 'The MIT License'
          url 'http://opensource.org/licenses/MIT'
          distribution 'repo'
        }
      }

      developers {
        developer {
          id 'ataxexe'
          name 'Marcelo Guimarães'
          email 'ataxexe@devnull.tools'
          organizationUrl 'http://devnull.tools'
          url 'http://entra.la'
        }
      }

      scm {
        connection 'scm:git:git@github.com:ataxexe/cafeina.git'
        url 'https://github.com/ataxexe/cafeina'
      }
    }
  }.writeTo(project.pomFile)
}

task dist(type: Zip,
    dependsOn: [clean, pom, jar, sourcesJar, javadocJar]) {
  from "$buildDir/libs"
  from project.pomFile
  from "changelog.md"
  from "README.md"
}

signing {
  sign configurations.archives
}
